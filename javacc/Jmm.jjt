options {
    LOOKAHEAD = 1;
    STATIC = false;
    NODE_SCOPE_HOOK=true;
}

PARSER_BEGIN(Jmm)
import java.util.ArrayList;
import java.util.List;
import report.Report;
import pt.up.fe.comp.jmm.report.Stage;
import pt.up.fe.comp.jmm.report.ReportType;

public class Jmm {
    private final List<Report> reports = new ArrayList<Report>();
    public static void main(String args[]) { }

    public void processWhileError(ParseException e) {
        Token currentToken = e.currentToken;
        Token errorToken = currentToken.next;
        this.reports.add(new Report(ReportType.ERROR, Stage.SYNTATIC, errorToken.beginLine, errorToken.endLine, getErrorMessage(this.reports.size(), errorToken, e.expectedTokenSequences, e.tokenImage)));
        recoverFromWhile();
    }

    public static String getErrorMessage(int errno, Token token,  int[][] expectedTokenSequences, String[] tokenImage) {
        int maxSize = 0;
        StringBuilder expected = new StringBuilder("\t\t");

        for (int i = 0; i < expectedTokenSequences.length; i++) {
            if (maxSize < expectedTokenSequences[i].length) {
                maxSize = expectedTokenSequences[i].length;
            }
            for (int j = 0; j < expectedTokenSequences[i].length; j++) {
                expected.append(tokenImage[expectedTokenSequences[i][j]]);
            }

            if(i != expectedTokenSequences.length - 1) {
                expected.append(" | ");
            } else {
                expected.append("\n");
            }
        }

        String info = String.format("%s\n%s\n%s",
            String.format("Error(%d) detected during parsing process. Unexpected token '%s' ....", errno, token),
            "\tError Details:",
            String.format("\t\tLine: %d \t Column: %d", token.beginLine, token.beginColumn)
        );

        return String.format("%s\n%s\n%s", info, "\tWas expecting:", expected);
      }

    public void recoverFromWhile() {
        Token t = getToken(0);
        do {
            if(t.kind == RPAR && getToken(1).kind != RPAR) { // If it finds the last ")"
                return;
            } else if(getToken(1).kind == LBRACKET) { // If it finds the first "{"
                return;
            }
            t = getNextToken();
        } while (true);
    }

    public List<Report> getReports() {
        return this.reports;
    }

    void jjtreeOpenNodeScope(Node n) {
        ((SimpleNode)n).put("LINE", Integer.toString(getToken(1).beginLine));
        ((SimpleNode)n).put("COLUMN", Integer.toString(getToken(1).beginColumn));
    }

    void jjtreeCloseNodeScope(Node n) { }
}
PARSER_END(Jmm)

SKIP: {
    " " | "\t" | "\r" | "\n"
    | < "/*" (~["*"])* "*" (~["*","/"] (~["*"])* "*" | "*")* "/" >  /*multiline comment*/
    | < "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> //inline comment
}

/*Reserved Words*/
TOKEN : {
    <IMPORT: "import">
    | <CLASS: "class" >
    | <EXTENDS: "extends">
    | <PUBLIC: "public">
    | <RETURN: "return">
    | <STATIC: "static">
    | <VOID: "void">
    | <MAIN: "main">
    | <STRING: "String">
    | <INT: "int">
    | <BOOLEAN: "boolean">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <LENGTH: "length">
    | <TRUE: "true" >
    | <FALSE: "false" >
    | <THIS: "this">
    | <NEW: "new">
}

/* Delimitaters*/
TOKEN : {
    < SEMICOLON: ";">
    | < COMMA: ",">
    | < LBRACKET: "{">
    | < RBRACKET: "}">
    | < LSQRBRACKET: "[">
    | < RSQRBRACKET: "]">
    | < LPAR: "(">
    | < RPAR: ")">
    | < DOT: ".">
}

/*Operators*/
TOKEN : {
    <PLUS: "+">
    | <MINUS: "-">
    | <MUL: "*">
    | <DIV: "/">
    | <LESS: "<">
    | <AND: "&&">
    | <NOT: "!">
    | <EQUALS: "=">
}

TOKEN: {
   <IDENTIFIER: ["a"-"z", "A"-"Z", "_", "$"](["a"-"z", "A"-"Z", "0"-"9", "_", "$"])* >

   | < INTEGER_LITERAL : <DECIMAL> | <HEXADECIMAL> | <BINARY> | <OCTAL> >
   | < #DECIMAL: (["0"-"9"])+ >
   | < #HEXADECIMAL: ("0x" | "0X")(["0"-"9", "A"-"F", "a"-"f"])+ >
   | < #BINARY: ("0b" | "0B")(["0", "1"])+ >
   | < #OCTAL: "0"(["0"-"7"])* >
}

SimpleNode Program(): { } {
    ImportDeclaration() ClassDeclaration() #Class { return jjtThis; }
}

void ImportDeclaration() #void: {} {
    (<IMPORT> ImportStatement() #Import <SEMICOLON>)*
}

void ImportStatement() #void: {} {
    Import() (<DOT>Import())*
}

Token Import() #void: {Token t;} {
    t=<IDENTIFIER> {jjtThis.put("VALUE", t.image);} #ImportName {return t;}
}

void ClassDeclaration() #void: {} {
    <CLASS> ClassBody() <LBRACKET> (VarDeclaration() #VarDecl)* (MethodDeclaration())* <RBRACKET>
}

void ClassBody() #void: {} {
    Name() [<EXTENDS> Extension()]
}

void VarDeclaration() #void: {} {
    Type() Name() <SEMICOLON>
}

void MethodDeclaration() #void: {} {
    <PUBLIC> (MainDeclaration() #Main | RegularMethodDeclaration() #Method)
}

void MainDeclaration() #void: {Token t;} {
    <STATIC> <VOID> <MAIN> <LPAR> (t=<STRING> {
        jjtThis.put("VALUE",  t.image);
    } #ClassName <LSQRBRACKET> <RSQRBRACKET>#Array(1) Name())#MethodParam(2) <RPAR>#MethodParameters(1) <LBRACKET>
        MethodBody()
    <RBRACKET>
}

void RegularMethodDeclaration() #void: {} {
    Type() Name() <LPAR> [MethodParameters()] <RPAR> <LBRACKET>
        MethodBody()
        <RETURN> Expression()#Return <SEMICOLON>
    <RBRACKET>
}

void MethodParameters() : {} {
    MethodParam() (<COMMA> MethodParam())*
}

void MethodParam() : {} {
    Type() Name()
}

void MethodBody() #void: {} {
    (LOOKAHEAD(2) VarDeclaration() #VarDecl)* (Statement())*
}

void Type() #void: {Token t;} {
    t=<BOOLEAN> {jjtThis.put("VALUE",  t.image);} #Boolean
    | t=<INT> {jjtThis.put("VALUE",  t.image);} #Int [<LSQRBRACKET> <RSQRBRACKET> #Array(1)]
    | t=<IDENTIFIER> {jjtThis.put("VALUE",  t.image);} #ClassName
}

void Statement() #void: {} {
    <LBRACKET> (Statement())* #StatementBlock <RBRACKET>
    | IfStatement()
    | WhileStatement()
    | AssignOrExpression() <SEMICOLON>
}

void IfStatement() #void: {} {
    <IF> <LPAR> Expression() <RPAR> Statement() <ELSE> Statement() #If(3)
}

/* *
* While Recovery
* https://www.cs.purdue.edu/homes/hosking/javacc/doc/errorrecovery.html
* https://www.cs.purdue.edu/homes/hosking/javacc/doc/apiroutines.html
* */
void WhileStatement() #void: {} {
    <WHILE>
    try {
        <LPAR> Expression() <RPAR>
    } catch (ParseException e) {
        processWhileError(e);
    }
    Statement() #While(2)
}

/* *
* EXPRESSION:
* https://introcs.cs.princeton.edu/java/11precedence/ -> Operators
* Operators listed in a javacc file go from the ones with least precedence to the ones with most precedence (from top to bottom)
* */
void Expression() #void: {} {
    LogicalAndExp()
}

void LogicalAndExp() #void: {} {
    RelationalLessExp() (<AND> RelationalLessExp() #LogicalAnd(2))*
}

void RelationalLessExp() #void: {} {
    ArithmeticAdditiveExp() (<LESS> ArithmeticAdditiveExp() #Less(2))*
}

void ArithmeticAdditiveExp() #void: {} {
    ArithmeticMultiplicativeExp()
    (<PLUS> ArithmeticMultiplicativeExp() #Add(2)
        | <MINUS> ArithmeticMultiplicativeExp() #Sub(2))*
}

void ArithmeticMultiplicativeExp() #void: {} {
    LogicalNotExp()
    (<MUL> LogicalNotExp() #Mul(2)
        | <DIV> LogicalNotExp() #Div(2))*
}

void LogicalNotExp() #void: {} {
    (<NOT> AccessExp() #Not)+ | AccessExp()
}

void AccessExp() #void: {} {
    TerminalExp() (ArrayAccess() #ArrayAccess(2) | <DOT> DotExp() #Dot(2))*
}

void DotExp() #void: {} {
    <LENGTH> #Length | CallWithParams() #MethodCall
}

void ArrayAccess() #void: {} {
    <LSQRBRACKET> Expression() #Index <RSQRBRACKET>
}

void CallWithParams() #void: {} {
    Name() Params()#Params
}

void TerminalExp() #void: {Token t;} {
    t=<INTEGER_LITERAL> {jjtThis.put("VALUE", t.image);} #IntegerVal
    | t=<TRUE> {jjtThis.put("VALUE", "true");} #Bool
    | t=<FALSE> {jjtThis.put("VALUE", "false");} #Bool
    | t=<THIS> {jjtThis.put("VALUE", "this");} #This
    | Var()
    | <NEW> NewInstance() #New
    | <LPAR> Expression() #Important <RPAR>
}

void AssignOrExpression() #void: {Token t;} {
    t=<INTEGER_LITERAL> {jjtThis.put("VALUE", t.image);} #IntegerVal Operations()
    | t=<TRUE> {jjtThis.put("VALUE", "true");} #Bool Operations()
    | t=<FALSE> {jjtThis.put("VALUE", "false");} #Bool Operations()
    | t=<THIS> {jjtThis.put("VALUE", "this");} #This Operations()
    | Var()  ArrayAccessExpressionOrAssign()
    | <NEW> NewInstance() #New
    | <LPAR> Expression() #Important <RPAR>
    | <NOT> LogicalNotExp() #Not
}

void ArrayAccessExpressionOrAssign() #void: {} {
    //Ignore warning, match with first ArrayAccess
    [LOOKAHEAD(1) ArrayAccess() #ArrayAccess(2)]
    (
        <EQUALS> Expression() #Assign(2)
            | Operations() (ArrayAccess() #ArrayAccess(2))*
    )
}

void Operations() #void: {} {
    RelationalLessOp() (<AND> RelationalLessOp() #LogicalAnd(2))*
}

void RelationalLessOp() #void: {} {
    ArithmeticAdditiveOp() (<LESS> ArithmeticAdditiveOp() #Less(2))*
}

void ArithmeticAdditiveOp() #void: {} {
    ArithmeticMultiplicativeOp()
        (<PLUS> ArithmeticMultiplicativeOp() #Add(2))*
        (<MINUS> ArithmeticMultiplicativeOp() #Sub(2))*
}

void ArithmeticMultiplicativeOp() #void: {} {
    AccessOp()
    (<MUL> AccessOp() #Mul(2))*
    (<DIV> AccessOp() #Div(2))*
}

void AccessOp() #void: {} {
    (<DOT> DotExp() #Dot(2))*
}

void Var(): {Token t;} {
    t=<IDENTIFIER> {jjtThis.put("VALUE", t.image);}
}

void Name(): {Token t;} {
    t=<IDENTIFIER> {jjtThis.put("VALUE", t.image);}
}

void Extension(): {Token t;} {
    t=<IDENTIFIER> {jjtThis.put("VALUE", t.image);}
}

void NewInstance() #void: {} {
    <INT> <LSQRBRACKET> Expression() #Size <RSQRBRACKET> #IntArray(1)
        | NewObject() #Object // new int[] | new Identifier()
}

void NewObject() #void: {} {
    Name() <LPAR><RPAR>
}

void Params() #void: {} {
    <LPAR> [Expression() #Param(1) (<COMMA> Expression() #Param(1))*] <RPAR>
}
